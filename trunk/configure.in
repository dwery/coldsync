dnl #
dnl # $Id: configure.in,v 1.16 1999-07-04 14:41:47 arensb Exp $
dnl #

dnl # XXX - 'make clean' doesn't work with Berkeley 'make': the
dnl # culprit is a couple of lines in the generated Makefile that say
dnl #	test "$var1" = value && var2=true
dnl # Berkeley 'make' copes quite well with the equivalent
dnl #	if test "$var1" = value; then var2=true; fi

dnl # XXX - Test to see whether cpp accepts macro definitions with
dnl # "..." as argument.

dnl # XXX - Test for MAXHOSTNAMELEN, MAXPATHLEN
dnl # XXX - See if printf() and friends accept %q format.
dnl # XXX - Under Solaris 2.5, gcc fusses if you print a 'uid_t' with
dnl # %ld. Figure out how to set this properly.
dnl # See AC_CHECK_SIZEOF(TYPE)

dnl # XXX - Check for existence of 'tar' (gtar, tar). If "tar
dnl # --version" exits with zero status and contains the string "GNU",
dnl # then it's GNU tar, and we can use
dnl #	GZIP="--best" ${TAR} chozf ${TARBALL} ${DISTDIR}
dnl # Otherwise, fall back on that old stand-by
dnl #	tar cf ${TARBALL} ${DISTDIR}
dnl #	${COMPRESS} ${TARBALL}
dnl # So also check for 'gzip'; if it doesn't exist, fall back on
dnl # 'compress'

AC_INIT(src/PConnection.c)

PACKAGE="coldsync"
VERSION="0.3.2"

AC_DEFINE_UNQUOTED(PACKAGE,["$PACKAGE"],[Name of package])
AC_DEFINE_UNQUOTED(VERSION,["$VERSION"],[Version number of package])
if test x"$EDITION" = x ; then
	EDITION="$VERSION"
fi
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(EDITION)

dnl # Checks for programs.

dnl # Find the C compiler
AC_PROG_CC

dnl # Find the C++ compiler
AC_PROG_CXX

# I know how to be pedantic with gcc. Other compilers, I'm not sure.
if test x"$GCC" = x"yes" ; then
		CFLAGS="$CFLAGS -Wall -ansi -pedantic"
else
	AC_MSG_RESULT(dnl
[	You don't seem to be using gcc, so various anal retentive
	compiler options won't be turned on. If you know what they
	are for your compiler, you may wish to turn them on, e.g.
		CFLAGS=\"-Wall -ansi -pedantic\" ./configure])
fi
AC_SUBST(CFLAGS)

AC_PROG_RANLIB
AC_PROG_INSTALL
AC_CHECK_PROG(MKID, mkid, mkid, true)
AC_SUBST(MKID)
AC_CHECK_PROGS(TAR,[gtar tar])
AC_CHECK_PROGS(GZIP,[gzip compress])

dnl # Checks for libraries.

dnl # Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h \
	sys/time.h \
	unistd.h \
	sys/types.h
])

dnl # Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_CHECK_SIZEOF(uid_t,uid_t)
AC_CHECK_SIZEOF(off_t,uid_t)
AC_HEADER_TIME
dnl AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl # Checks for library functions.
AC_CHECK_FUNCS(select \
	cfmakeraw
)

# XXX - Under RedHat, if you compile with "gcc -ansi", it complains
# about "implicit declaration of cfmakeraw()". You can fix this with
# "-D_BSD_SOURCE", but is there a better way of turning this on?
# Ditto for mkstemp()
# XXX - Under RedHat 5.2 Linux, mkstemp() requires <stdlib.h>,
# although the man page just says <unistd.h>

# Tell the various Makefiles to use the compatibility function provided
if test x"$ac_cv_func_cfmakeraw" = xno ; then
	COMPAT="$COMPAT cfmakeraw.c"
	COMPATOBJS="$COMPATOBJS cfmakeraw.o"
fi

AC_SUBST(COMPAT)
AC_SUBST(COMPATOBJS)

dnl # Should print out an error if one or more required components
dnl # couldn't be found. Use AC_MSG_WARN([message])

#if test x"$ac_cv_header_sys_frobozz_h" = x"no" ; then
#	AC_MSG_WARN([<frobozz.h> could not be found. This package may not
#	compile properly])
#fi

if test x"$ac_cv_func_select" = x"no" ; then
	AC_MSG_WARN([The select() function not be found. This package may not compile
properly])
fi

AC_CONFIG_HEADER(config.h)
AC_OUTPUT([Makefile
	src/Makefile
	doc/Makefile
	doc/version.texi])
